//=====================================
// Query Name: Anomalous IPC$ Recon Detection
// Author: Sameer P Sheik
// Version: 2.0
//=====================================
// Description: 
// Detects anomalous attempts to access the IPC$ share using Event ID 5140.
// This may indicate lateral movement or network reconnaissance by attackers.
//=====================================
// Severity Mapping:
// - Critical: Multiple distinct computers accessing IPC$ from a single account in a short timeframe.
// - High: A single privileged account accessing IPC$ on multiple machines.
// - Medium: A regular user accessing IPC$ on multiple machines.
// - Low: Normal administrative activity (filtered by allowlist).
//=====================================
// MITRE ATT&CKÂ® Mapping:
// - T1021: Remote Services (IPC$ enumeration for lateral movement)
// - T1071.001: Application Layer Protocol (SMB-based communication)
// - T1135: Network Share Discovery (Reconnaissance via IPC$)
//=====================================
// Validation Against OOB Analytics:
// - Complements Sentinel UEBA for anomaly-based account modifications
// - Enhances Microsoft Defender for Identity detections on SMB recon
// - Works with Windows Security Analytics (EventID 5140 tracking)
//=====================================

let starttime = 360d;
let timeframe = 6h; // Increased from 30m to ensure enough data
let detectionthreshold = 2;
let outliers = 
SecurityEvent
| where TimeGenerated > ago(starttime)
| where EventID == "5140"
| where ShareName contains "IPC$"  // Fixed ShareName filter
| where Account !in ("DOMAIN\\Account1")  // Exclude known admin/service accounts
| summarize Events=count() by Account, bin(TimeGenerated, timeframe)
| summarize EventCount=make_list(Events), TimeGenerated=make_list(TimeGenerated) by Account
| extend outliers=series_decompose_anomalies(EventCount, detectionthreshold)
| mv-expand TimeGenerated, EventCount, outliers
| where outliers == 1
| distinct Account;

SecurityEvent
| where TimeGenerated > ago(timeframe)
| where EventID == "5140"
| where ShareName contains "IPC$" // Ensure all IPC$ access attempts are captured
| parse Account with * "\\" AccountParse "$"
| parse Computer with ComputerParse "." * 
| where AccountParse != ComputerParse  // Exclude self-access events
| where Account in (outliers)  // Identify anomalous activity
| extend Severity = case(
    Account startswith "admin", "High",
    Account contains "$", "Critical",
    "Medium"
)
| summarize AccountActivity=make_set(Computer) by Account, Severity
